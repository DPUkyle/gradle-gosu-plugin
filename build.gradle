plugins {
	id 'com.gradle.plugin-publish' version '0.9.1'
    id 'maven-publish'
	id 'java'
}

group = 'org.gosu-lang.gosu'
version = project.gosuVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	jcenter()
    mavenLocal() //TODO delete me before publishing
}

dependencies {
    compile gradleApi()
    compile group: 'org.gosu-lang.gosu', name: 'gosu-core-api', version: project.gosuVersion
    compile group: 'org.gosu-lang.gosu', name: 'gosu-core', version: project.gosuVersion // compilation will require this jar at compile-time, not runtime
    testCompile group: 'junit', name: 'junit', version: project.junitVersion
    testCompile gradleTestKit()
    runtime group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.0.22'
}

pluginBundle {
    website = 'http://gosu-lang.org'
    vcsUrl = 'https://github.com/gosu-lang/gradle-gosu-plugin'
    description = 'Gosu language compiler for Gradle'
    tags = ['gosu']

    plugins {
        gosuPlugin {
            id = 'org.gosu-lang.gosu'
            displayName = 'Gosu Gradle Plugin'
            version = '0.1-alpha'
        }
    }
}

// Write the plugin's classpath to a file to share with the tests: build/createClasspathManifest/plugin-classpath.txt
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join('\n')
    }
}

task gosuVersionHelper {
    def outputDir = file("$buildDir/$name")

    def propsFile = new File(project.rootDir, 'gradle.properties')

    inputs.files propsFile
    outputs.dir outputDir

    doLast {
        outputDir.mkdir()
        def props = new Properties()
        props.load(new BufferedReader(new FileReader(propsFile)))
        file("$outputDir/gosuVersion.txt").text = props.getProperty('gosuVersion')
    }
}

dependencies {
    testRuntime files(createClasspathManifest) // Add the classpath file to the test runtime classpath
    testRuntime files(gosuVersionHelper) // Persist a file containing the gosuVersion
}

task sourceJar(type: Jar) {
    description 'generate sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    description 'generate javadoc'
    from javadoc
}

/* from 'maven-publish' */
publishing {
    publications {
        pluginArtifact(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

/**
 * Fetches the publishing credentials from the build machine's env vars
 */
task setupPluginUpload << {

    def key = System.env.gradlePublishKey
    def secret = System.env.gradlePublishSecret

    if( !key || !secret ) {
        throw new RuntimeException('gradlePublishKey and/or gradlePublishSecret are not defined environment variables')
    }

    System.properties.setProperty('gradle.publish.key', key)
    System.properties.setProperty('gradle.publish.secret', secret)
}

tasks.publishPlugins.dependsOn tasks.setupPluginUpload